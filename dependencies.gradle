dependencies {
    ext {
        def isTestProject = project.name.endsWith("-test")
        commonTestDependency = { args ->
            if (isTestProject) {
                commonMainApi args
            } else {
                commonTestImplementation args
            }
        }
        jvmCommonTestDependency = { args ->
            if (isTestProject) {
                jvmCommonMainApi args
            } else {
                jvmCommonTestImplementation args
            }
        }

        if (!project.plugins.hasPlugin("org.jetbrains.kotlin.multiplatform")) {
            commonMainApi = { args -> api args }
            commonTestImplementation = { args -> testImplementation args }
        }

        versionConstraints = {
            commonMainApi platform("org.jetbrains.kotlinx:kotlinx-coroutines-bom:1.5.2")
        }

        base = {
            versionConstraints()

            commonMainApi "org.jetbrains.kotlinx:kotlinx-coroutines-core"

            commonTestDependency "org.jetbrains.kotlin:kotlin-test"

            jvmCommonTestDependency "org.jetbrains.kotlin:kotlin-test-junit"
            jvmCommonTestDependency "junit:junit:4.13.2"
            jvmCommonTestDependency "org.jetbrains.kotlinx:kotlinx-coroutines-test"

            jvmCommonTestImplementation "io.mockk:mockk:1.12.0"
        }

        activityVersion = "1.4.0"
        fragmentVersion = "1.4.0"
        lifecycleVersion = "2.4.0"
        androidBase = {
            androidMainApi "androidx.annotation:annotation:1.3.0"
            androidMainApi "androidx.appcompat:appcompat:1.4.0"
            androidMainApi "androidx.core:core-ktx:1.7.0"
            androidMainApi "androidx.activity:activity-ktx:$activityVersion"
            androidMainApi "androidx.fragment:fragment-ktx:$fragmentVersion"

            androidMainApi "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVersion"
            androidMainApi "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
            androidMainApi "androidx.lifecycle:lifecycle-service:$lifecycleVersion"
            androidMainApi "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
            constraints {
                api "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"
                api "androidx.lifecycle:lifecycle-service:$lifecycleVersion"
                api "androidx.lifecycle:lifecycle-process:$lifecycleVersion"
            }

            androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
            androidTestImplementation "androidx.fragment:fragment-testing:$fragmentVersion"
            androidTestImplementation "androidx.test:core-ktx:1.4.0"
            androidTestImplementation "androidx.test.ext:junit-ktx:1.1.3"
            androidTestImplementation "org.robolectric:robolectric:4.6.1"
        }

        composeVersion = "1.1.0-rc01"
        jetpackCompose = {
            android {
                buildFeatures {
                    compose true
                }

                composeOptions {
                    kotlinCompilerExtensionVersion = composeVersion
                }
            }

            androidMainApi "androidx.activity:activity-compose:$activityVersion"
            androidMainApi "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycleVersion"

            androidMainApi "androidx.compose.runtime:runtime:$composeVersion"
            constraints {
                api "androidx.compose.foundation:foundation:$composeVersion"
                api "androidx.compose.ui:ui:$composeVersion"
                api "androidx.compose.animation:animation:$composeVersion"
                api "androidx.compose.material:material:$composeVersion"
                api "androidx.compose.ui:ui-tooling:$composeVersion"
            }
        }
    }
}
