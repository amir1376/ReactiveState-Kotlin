dependencies {
    ext {
        def isTestProject = project.name.endsWith("-test")
        commonTestDependency = { args ->
            if (isTestProject) {
                commonMainApi args
            } else {
                commonTestImplementation args
            }
        }
        jvmCommonTestDependency = { args ->
            if (isTestProject) {
                jvmCommonMainApi args
            } else {
                jvmCommonTestImplementation args
            }
        }
        androidTestDependency = { args ->
            if (isTestProject) {
                androidMainApi args
            } else {
                androidUnitTestImplementation args
            }
        }

        if (!isMultiplatform) {
            commonMainApi = { args -> api args }
            commonTestImplementation = { args -> testImplementation args }
        }

        versionConstraints = {
            commonMainApi platform("org.jetbrains.kotlinx:kotlinx-coroutines-bom:1.6.4")
        }

        base = {
            versionConstraints()

            commonMainApi "org.jetbrains.kotlinx:kotlinx-coroutines-core"

            commonTestDependency "org.jetbrains.kotlin:kotlin-test"

            jvmCommonTestDependency "org.jetbrains.kotlin:kotlin-test-junit"
            jvmCommonTestDependency "junit:junit:4.13.2"
            commonTestDependency "org.jetbrains.kotlinx:kotlinx-coroutines-test"

            jvmCommonTestImplementation "io.mockk:mockk:1.13.3"
        }

        activityVersion = "1.6.1"
        fragmentVersion = "1.5.5"
        lifecycleVersion = "2.5.1"
        androidBase = {
            androidMainApi "androidx.annotation:annotation:1.5.0"
            androidMainApi "androidx.appcompat:appcompat:1.5.1"
            androidMainApi "androidx.core:core-ktx:1.9.0"
            androidMainApi "androidx.activity:activity-ktx:$activityVersion"
            androidMainApi "androidx.fragment:fragment-ktx:$fragmentVersion"

            androidMainApi "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVersion"
            androidMainApi "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
            androidMainApi "androidx.lifecycle:lifecycle-service:$lifecycleVersion"
            androidMainApi "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
            constraints {
                api "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"
                api "androidx.lifecycle:lifecycle-service:$lifecycleVersion"
                api "androidx.lifecycle:lifecycle-process:$lifecycleVersion"
            }

            androidTestDependency "androidx.arch.core:core-testing:2.1.0"
            androidTestDependency "androidx.fragment:fragment-testing:$fragmentVersion"
            androidTestDependency "androidx.test:core-ktx:1.5.0"
            androidTestDependency "androidx.test.ext:junit-ktx:1.1.5"
            androidTestDependency "org.robolectric:robolectric:4.9.2"
        }

        composeVersion = "1.3.0-rc01"
        composeCompilerVersion = "1.3.2"
        jetpackCompose = {
            android {
                buildFeatures {
                    compose = true
                }

                composeOptions {
                    kotlinCompilerExtensionVersion = composeCompilerVersion
                }
            }

            androidMainApi "androidx.activity:activity-compose:$activityVersion"
            androidMainApi "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycleVersion"

            androidMainApi "androidx.compose.runtime:runtime:$composeVersion"
            constraints {
                api "androidx.compose.foundation:foundation:$composeVersion"
                api "androidx.compose.ui:ui:$composeVersion"
                api "androidx.compose.animation:animation:$composeVersion"
                api "androidx.compose.material:material:$composeVersion"
                api "androidx.compose.ui:ui-tooling:$composeVersion"
            }
        }
    }
}
