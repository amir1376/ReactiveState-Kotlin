dependencies {
    ext {
        def isTestProject = project.name.endsWith("-test")
        commonTestDependency = { args ->
            if (isTestProject) {
                commonMainApi args
            } else {
                commonTestImplementation args
            }
        }
        jvmCommonTestDependency = { args ->
            if (isTestProject) {
                jvmCommonMainApi args
            } else {
                jvmCommonTestImplementation args
            }
        }
        androidTestDependency = { args ->
            if (isTestProject) {
                androidMainApi args
            } else {
                androidTestImplementation args
            }
        }

        if (!isMultiplatform) {
            commonMainApi = { args -> api args }
            commonTestImplementation = { args -> testImplementation args }
        }

        versionConstraints = {
            commonMainApi platform("org.jetbrains.kotlinx:kotlinx-coroutines-bom:1.8.1")
        }

        base = {
            versionConstraints()

            commonMainApi "org.jetbrains.kotlinx:kotlinx-coroutines-core"

            commonTestDependency "org.jetbrains.kotlin:kotlin-test"

            jvmCommonTestDependency "org.jetbrains.kotlin:kotlin-test-junit"
            jvmCommonTestDependency "junit:junit:4.13.2"
            commonTestDependency "org.jetbrains.kotlinx:kotlinx-coroutines-test"

            jvmCommonTestImplementation "io.mockk:mockk:1.13.5"
        }

        activityVersion = "1.8.2"
        fragmentVersion = "1.5.7"
        lifecycleVersion = "2.8.0"
        androidBase = {
            androidMainApi "androidx.annotation:annotation:1.6.0"
            androidMainApi "androidx.appcompat:appcompat:1.6.1"
            androidMainApi "androidx.core:core-ktx:1.10.1"
            androidMainApi "androidx.activity:activity-ktx:$activityVersion"
            androidMainApi "androidx.fragment:fragment-ktx:$fragmentVersion"

            androidMainApi "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVersion"
            androidMainApi "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
            androidMainApi "androidx.lifecycle:lifecycle-service:$lifecycleVersion"
//            androidMainApi "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
            constraints {
                api "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"
                api "androidx.lifecycle:lifecycle-service:$lifecycleVersion"
                api "androidx.lifecycle:lifecycle-process:$lifecycleVersion"
            }

            androidTestDependency "androidx.arch.core:core-testing:2.2.0"
            androidTestDependency "androidx.fragment:fragment-testing:$fragmentVersion"
            androidTestDependency "androidx.test:core-ktx:1.5.0"
            androidTestDependency "androidx.test.ext:junit-ktx:1.1.5"
            androidTestDependency "org.robolectric:robolectric:4.11.1"
        }

        jetpackCompose = {
            api platform('androidx.compose:compose-bom:2024.08.00')
            composeCompiler {
                enableStrongSkippingMode = true
            }

            androidMainApi "androidx.activity:activity-compose:$activityVersion"
            androidMainApi "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycleVersion"

            commonMainApi compose.runtime
        }
    }
}
