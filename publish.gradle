// Based on:
// * https://gist.github.com/Robyer/a6578e60127418b380ca133a1291f017
// * https://stackoverflow.com/questions/26874498/publish-an-android-library-to-maven-with-aar-and-source-jar

apply from: "$rootDir/secrets.gradle"

group groupId

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

apply plugin: 'org.jetbrains.dokka'
dokka {
    outputDirectory = "$buildDir/dokka"
    outputFormat = 'html'
}

apply plugin: "maven-publish"
// Because the components are created only during the afterEvaluate phase, we must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifact androidSourcesJar
            }
        }
    }
    repositories {
        maven {
            name = "Bintray"
            url = uri("https://api.bintray.com/maven/$bintrayUserOrg/${bintrayRepo}/${group}:${project.name}")
            credentials {
                username = secrets.getProperty("bintray.username")
                password = secrets.getProperty("bintray.apikey")
            }
        }
    }
}

apply plugin: "com.jfrog.bintray"
bintray {
    user = secrets.getProperty("bintray.username")
    key = secrets.getProperty("bintray.apikey")
    publications = ['release']
    pkg {
        userOrg = bintrayUserOrg
        repo = bintrayRepo
        name = "$group:${project.name}"
        licenses = rootProject.licenses
        vcsUrl = rootProject.vcsUrl
        websiteUrl = rootProject.website
    }
}
